SELECT DISTINCT ?item ?itemID ?dateModified ?itemLabel
?main_subjectLabel ?language_usedLabel ?short_nameLabel ?official_website 
?Twitter_username ?WikiCFP_conference_series_ID
?GND_ID ?Freebase_ID ?event_interval_inmonths

WHERE {
	?item wdt:P31 wd:Q47258130;
	    schema:dateModified ?dateModified;
	    rdfs:label ?item_label.
	    	FILTER (LANG(?item_label) = "en"). # although SERVICE wikibase:label is used to get the ?itemLabel
                                               # this will prevent titles that are not in EN, to appear in results
    BIND(STRAFTER(STR(?item), 'http://www.wikidata.org/entity/') AS ?itemID)
	OPTIONAL { ?item wdt:P921 ?main_subject . }
	OPTIONAL { ?item wdt:P2936 ?language_used . }
	OPTIONAL { ?item wdt:P1813 ?short_name . }
	OPTIONAL { ?item wdt:P856 ?official_website . }
	OPTIONAL { ?item wdt:P2002 ?Twitter_username . }
	OPTIONAL { ?item wdt:P227 ?GND_ID . }
	OPTIONAL { ?item wdt:P5127 ?WikiCFP_conference_series_IDvalue . }
	OPTIONAL { ?item wdt:P5127 ?WikiCFP_conference_series_IDvalue .
		BIND ( CONCAT("http://wikicfp.com/cfp/program?id=", STR(?WikiCFP_conference_series_IDvalue)) AS ?WikiCFP_conference_series_ID )
	}
	OPTIONAL { ?item wdt:P646 ?Freebase_IDvalue .
	     BIND ( CONCAT("https://freebase.toolforge.org", STR(?Freebase_IDvalue)) AS ?Freebase_ID )
	}
	OPTIONAL {
		?item p:P2257 [
		    	a wikibase:BestRank;  # best ranked unit returned
				psv:P2257 [
					wikibase:quantityAmount ?event_interval;
					wikibase:quantityUnit ?event_interval_unit
				]
			].
	   # if unitLabel is wd:Q577 (year) or wd:Q1092296 (annum): *
	   BIND( IF (?event_interval_unit in (wd:Q577, wd:Q1092296 ), ?event_interval * 12, ?event_interval) AS ?event_interval_inmonths ) .
	}
	SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY DESC(?dateModified)